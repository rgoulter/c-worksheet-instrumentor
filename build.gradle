plugins {
  id 'java'
  id 'scala'
  id 'antlr'
  id 'application'
  id 'com.github.maiflai.scalatest' version '0.32'
}

group = "edu.nus"
version = "0.2.6-SNAPSHOT"

base {
  archivesName = "worksheetify.instrumentor"
}

application {
  mainClass = 'edu.nus.worksheet.Worksheetify'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.12.18'
    implementation 'io.argonaut:argonaut_2.12:6.2'

    implementation 'org.antlr:antlr4-runtime:4.5'
    implementation 'org.antlr:ST4:4.0.8'

    testImplementation 'org.scalatest:scalatest_2.12:3.2.17'
    testImplementation 'com.vladsch.flexmark:flexmark-all:0.64.8'

    antlr 'org.antlr:antlr4:4.5'
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'edu.nus.worksheet.instrumentor']
}

test {
    maxParallelForks = 1
}

jar {
    manifest {
        attributes(
            'Bundle-Version': '0.1.0.qualifier',
            'Export-Package': 'edu.nus.worksheet, edu.nus.worksheet.instrumentor, edu.nus.worksheet.instrumentor.templates'
        )
    }
}

task worksheetifyServerStartScripts(type: CreateStartScripts) {
    mainClass = "edu.nus.worksheet.WorksheetifyServer"
    applicationName = "c-worksheetify-server"
    outputDir = file("$buildDir/scripts")
    classpath = jar.outputs.files + configurations.runtimeClasspath
}

distTar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

distZip {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

installDist {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

distributions {
    main {
        contents {
            into("bin") {
                from(worksheetifyServerStartScripts) {
                    fileMode 0755
                }
            }
        }
    }
}
